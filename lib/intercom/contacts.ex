defmodule Intercom.Contacts do
  @moduledoc """
  Provides functionality for managing contacts.

  See https://developers.intercom.com/intercom-api-reference/reference#contacts-model
  """

  @doc """
  Retrieves one contact identified by the intercom id

  Arguments:
  - `intercom_id`: The id generated by intercom for the user

  Returns {:ok, data, metadata} or {:error, error_code, metadata}
  """
  @spec get(String.t()) :: Intercom.API.response()
  def get(intercom_id) do
    Intercom.API.call_endpoint(:get, "contacts/#{intercom_id}")
  end

  @doc """
  Retrieves a list of contacts with `field` equal to `value`

  Arguments:
  - `field`: The intercom field, see https://developers.intercom.com/intercom-api-reference/reference#search-for-contacts
  - `value`: The value the intercom field must have to match

  Returns {:ok, data, metadata} or {:error, error_code, metadata}
  """
  @spec find_equal(String.t(), String.t()) :: Intercom.API.response()
  def find_equal(field, value) do
    Intercom.API.call_endpoint(:post, "contacts/search", %{
      query: %{field: field, operator: "=", value: value}
    })
  end
end
